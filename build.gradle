import org.yaml.snakeyaml.DumperOptions
import org.yaml.snakeyaml.Yaml

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath group: 'org.yaml', name: 'snakeyaml', version: '1.27'
    }
}



plugins {
    id "java"
    id "com.github.johnrengelman.shadow" version "6.1.0"
    id "maven-publish"
    id "io.freefair.lombok" version "5.3.0"
}




group = "com.dumbdogdiner"
version = "3.3.2"


repositories {
    mavenCentral()
    google()

    // spigot repository
    maven { url = "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url = "https://papermc.io/repo/repository/maven-public/" }
    maven { url = "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
    maven { url = "https://jitpack.io" }
    maven {
        credentials {
            username "$ghUser"
            password "$ghPass"
        }
        url = "https://maven.pkg.github.com/DumbDogDiner/StickyAPI" }
    maven {
        credentials {
            username "$ghUser"
            password "$ghPass"
        }
        url = "https://maven.pkg.github.com/DumbDogDiner/closedsource-package-mirror" }

    //for testing stuff locally, do not remove plz
    flatDir {dirs 'libs'}
}

dependencies {
    // java deps
    compileOnly "org.projectlombok:lombok:1.18.16"
    annotationProcessor 'org.projectlombok:lombok:1.18.12'
    implementation 'org.jetbrains:annotations:20.1.0'

    // spigot, paper
    compileOnly "org.spigotmc:spigot-api:1.16.3-R0.1-SNAPSHOT"
    compileOnly "com.destroystokyo.paper:paper-api:1.16.3-R0.1-SNAPSHOT"

    // plugin-specific deps
    compileOnly "com.github.MilkBowl:VaultAPI:1.7"
    compileOnly "me.clip:placeholderapi:2.10.6"
    implementation "com.dumbdogdiner:stickyapi:2.0.0"
    implementation 'com.atlassian.commonmark:commonmark:0.15.2'
    compileOnly 'net.luckperms:api:5.2'
    compileOnly 'com.dumbdogdiner.closedsource-package-mirror:stafffacilities:4.8.5'
    //compileOnly 'me.xtomyserrax:StaffFacilities'
    //compile fileTree(dir: 'libs', include: '*.jar')
}

task config() {
    def plugin = new Yaml().load( new File("src/main/resources/plugin.yml").newInputStream().getText() )

    plugin.version = version

    def yamlopts = new DumperOptions()
    yamlopts.setDefaultFlowStyle(DumperOptions.FlowStyle.BLOCK);

    new File("src/main/resources/plugin.yml").write(new Yaml(yamlopts).dump(plugin))
}


tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

tasks.build.dependsOn tasks.shadowJar, config
shadowJar {
    archiveClassifier.set("")
    project.configurations.implementation.canBeResolved = true
    configurations = [project.configurations.implementation]
}

task sources(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

tasks.publish.dependsOn build, sources

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/DumbDogDiner/StickyCommands")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
            artifact sources // Publish the output of the sources task
        }
    }
}
